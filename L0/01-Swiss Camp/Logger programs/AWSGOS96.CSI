;{CR10}
; --------------------------------------------------------------
; Program: awsgos96.csi (cr10x) for the Swiss Camp
; to be loaded to GOES aws installed in 1996.
; Program written by: Ted DeMaria; Jason Box; and Dan Anderson. 4-96
; Modified by K. Steffen 6-11-96
; --------------------------------------------------------------
; This program measures various meteorological instruments
; NOTE: this program is written for the cr10x not cr10
; The cr10 will take a program called awsarg96.doc
; --------------------------------------------------------------

*Table 1 Program
  01: 15.0      Execution Interval (seconds)
; 15 second suggested execution interval

1:  Set Port(s) (P20)
 1: 9999     C8,C7,C6,C5 Options
 2: 9933     C4,C3,C2,C1 Options
; Configure control ports so that 1 and 2 are
; milisecond pulses

2:  Do (P86)
 1: 42       Set Port 2 High
; send voltage from control port 2 to multiplexer
; to turn on multiplexer

3:  Full Bridge (P6)
 1: 1        Reps
 2: 22       ñ 7.5 mV 60 Hz Rejection Range
 3: 1        DIFF Channel
 4: 1        Excite all reps w/Exchan 1
 5: 350      mV Excitation
 6: 1        Loc [ full_brg  ]
 7: -.001    Mult
 8: .09707   Offset
; measures the output of the Full Bridge
; -> (internal multiplexer reference temperature signal)

4:  BR Transform Rf[X/(1-X)] (P59)
 1: 1        Reps
 2: 1        Loc [ full_brg  ]
 3: 10.025   Multiplier (Rf)
; Find the resistance of the reference temperature probe

5:  Temperature RTD (P16)
 1: 1        Reps
 2: 1        R/RO Loc [ full_brg  ]
 3: 1        Loc [ full_brg  ]
 4: 1        Mult
 5: 0.0000   Offset
; calculate the reference temperature

6:  Do (P86)
 1: 71       Pulse Port 1
; send voltage from control port 1 to multiplexer

7:  Do (P86)
 1: 71       Pulse Port 1
; same as above, must do 2 x to step forward one
; multiplexer input channel.

8:  Volt (Diff) (P2)
 1: 1        Reps
 2: 4        ñ 250 mV Slow Range
 3: 1        DIFF Channel
 4: 2     -- Loc [ net_rad   ]
 5: 1        Mult
 6: 0        Offset
; Net Radiometer

9:  Beginning of Loop (P87)
 1: 0        Delay
 2: 2        Loop Count

10:  Do (P86)
 1: 71       Pulse Port 1
; send voltage from control port 1 to multiplexer
; moves measurement to the next mplexer port

11:  Do (P86)
 1: 71       Pulse Port 1
; same as above, must do it twice

12:  Volt (Diff) (P2)
 1: 1        Reps
 2: 4        ñ 250 mV Slow Range
 3: 1        DIFF Channel
 4: 3     -- Loc [ THR_up    ]
 5: 1        Mult
 6: 0        Offset
; THR Thermopile upward-looking

13:  End (P95)
; end of THR loop (up and down)

14:  Do (P86)
 1: 71       Pulse Port 1

15:  Do (P86)
 1: 71       Pulse Port 1

16:  Ex-Del-Diff (P8)
 1: 1        Reps
 2: 5        ñ 2500 mV Slow Range
 3: 1        DIFF Channel
 4: 2        Excite all reps w/Exchan 2
 5: 1        Delay (units 0.01 sec)
 6: 2500     mV Excitation
 7: 5        Loc [ THR_res   ]
 8: 1.0      Mult
 9: 0.0      Offset
; THR precision resistor

17:  Beginning of Loop (P87)
 1: 0        Delay
 2: 2        Loop Count

18:  Do (P86)
 1: 71       Pulse Port 1

19:  Do (P86)
 1: 71       Pulse Port 1

20:  Volt (Diff) (P2)
 1: 1        Reps
 2: 1        ñ 2.5 mV Slow Range
 3: 1        DIFF Channel
 4: 6     -- Loc [ THR_T_up  ]
 5: 1.0      Mult
 6: 0.0      Offset
; THR Dome Temperature

21:  End (P95)
; end of THR loop (Dome temp up and down)


22:  Do (P86)
 1: 71       Pulse Port 1

23:  Do (P86)
 1: 71       Pulse Port 1

24:  Volt (Diff) (P2)
 1: 1        Reps
 2: 1        ñ 2.5 mV Slow Range
 3: 1        DIFF Channel
 4: 8     -- Loc [ L_in      ]
 5: 1.0      Mult
 6: 0.0      Offset

25:  Beginning of Loop (P87)
 1: 0        Delay
 2: 10       Loop Count
; begin thermocople multiplexer measurement loop

26:  Do (P86)
 1: 71       Pulse Port 1
; send voltage from control port 1 to multiplexer

27:  Do (P86)
 1: 71       Pulse Port 1
; same as above, must do it twice

28:  Thermocouple Temp (DIFF) (P14)
 1: 1        Reps
 2: 01       ñ 2.5 mV Slow Range
 3: 1        DIFF Channel
 4: 1        Type T (Copper-Constantan)
 5: 1        Ref Temp Loc [ full_brg  ]
 6: 9     -- Loc [ tc1       ]
 7: 1        Mult
 8: 0        Offset
; Sub-surface Ice Thermocouple string (0.0 m .. 9.0 m initial)

29:  End (P95)
; end loop of 10, thermocouple measurement loop.

30:  Do (P86)
 1: 71       Pulse Port 1

31:  Do (P86)
 1: 71       Pulse Port 1

32:  Beginning of Loop (P87)
 1: 0        Delay
 2: 2        Loop Count
; begin air temperature thermocouple multiplexer loop

33:  Thermocouple Temp (DIFF) (P14)
 1: 1        Reps
 2: 01       ñ 2.5 mV Slow Range
 3: 1        DIFF Channel
 4: 2        Type E (Chromel-Constantan)
 5: 1        Ref Temp Loc [ full_brg  ]
 6: 19    -- Loc [ tc_air1   ]
 7: 1        Mult
 8: 0        Offset
; 2 x temperature mesurement 

34:  End (P95)
; end air temperature thermocouple multiplexer loop

35:  Beginning of Loop (P87)
 1: 0        Delay
 2: 6        Loop Count

36:  Do (P86)
 1: 71       Pulse Port 1

37:  Do (P86)
 1: 71       Pulse Port 1

38:  Thermocouple Temp (DIFF) (P14)
 1: 1        Reps
 2: 01       ñ 2.5 mV Slow Range
 3: 1        DIFF Channel
 4: 1        Type T (Copper-Constantan)
 5: 1        Ref Temp Loc [ full_brg  ]
 6: 35    -- Loc [ TC_snow1  ]
 7: 1.0      Mult
 8: 0.0      Offset
; Thermocouples in the Snow

39:  End (P95)
;end TC_snow loop (6x)

40:  Do (P86)
 1: 52       Set Port 2 Low
; turns multiplexer off, the remaining measurements
; will occur on the cr10 and not on the multiplexer.

41:  If time is (P92)
 1: 00       Minutes (Seconds --) into a
 2: 60       Interval (same units as above)
 3: 10       Set Output Flag High
; send data to final storage each hour 
; xx parameters are to be measured and averaged,
; xx + JD and time = xx, the maximum
; number of parameters GOES will transmit.

42:  Set Active Storage Area (P80)
 1: 01       Final Storage Area 1
 2: 11       Array ID or Loc [ _________ ]
; set station array id number, 01 = swiss camp,
; 02 = crawford point, ... in the order that they
; were installed.  So, field season 1996, first AWS
; will be number 06.
; set array id = station number plus block number
; array id # = station number (06) + block number (2) -> id#=062

; List of AWS ID Codes
; ---------------------
; Swiss Camp   = AWS 01
; Crawford Pt  = AWS 02
; NASA-U       = AWS 03
; GITS, CAMP C = AWS 04
; Humboldt     = AWS 05
; NASA-E       = AWS 06
; SUMMIT       = AWS 07
; TUNU         = AWS 08
; JAR          = AWS 09

43:  Real Time (P77)
 1: 0110     Day,Hour/Minute

44:  Average (P71)
 1: 7        Reps
 2: 2        Loc [ net_rad   ]
; net, THR_up, THR_do, THR_res, THR_T_up, THR_T_do, L_in: loc 2-8

45:  Average (P71)
 1: 2        Reps
 2: 19       Loc [ tc_air1   ]
; 2 x air temperature thermocouple averages, loc 19-20

46:  Average (P71)
 1: 4        Reps
 2: 23       Loc [ rh_1      ]
; 2 x cs-500 relative humidity averages, loc 23-24
; 2 x wind speed averages, loc 25 - 26

47:  Average (P71)
 1: 1        Reps
 2: 27       Loc [ U_dir1    ]
; 1 x wind direction average from the higher wind monitor, loc 27

48:  Average (P71)
 1: 3        Reps
 2: 28       Loc [ pressure  ]
; 1 x pressure average, loc 29
; 2 x snow height averages, loc 30-31

49:  Average (P71)
 1: 1        Reps
 2: 18       Loc [ tc10      ]
; there is one channel left out of 14, so lets measure
; snow temperature at 0 m.

50:  Average (P71)
 1: 10       Reps
 2: 9        Loc [ tc1       ]
; ice thermocouple measurements tc1 - tc10

51:  Average (P71)
 1: 6        Reps
 2: 35       Loc [ TC_snow1  ]
; snow therocouples TC_snow1 - TC_snow6

52:  Average (P71)
 1: 2        Reps
 2: 21       Loc [ tair_1    ]
; CS-500 air temperature measurements 1 and 2

53:  Average (P71)
 1: 1        Reps
 2: 42       Loc [ BATTERY   ]

54:  If time is (P92)
 1: 0        Minutes (Seconds --) into a
 2: 60       Interval (same units as above)
 3: 30       Then Do

55:  Data Transfer to GOES (P120)
 1: 0        self-timed buffer/append new data to old data
 2: 43       FWD/Ref Power Loc [ Antenna   ]

56:  End (P95)
; end second buffer transmission

*Table 2 Program
  02: 60.0      Execution Interval (seconds)
; 60 second suggested sampling interval
; in 1995 this was 600, but filled memory with wind
; sensor pulses.
; if we set exec to 60 sec, we will not overfill the 
; memory buffer, yet we can still measure certain sensors
; on a 600 sec interval.

1:  Do (P86)
 1: 43       Set Port 3 High
; voltage control port 3, jumped to switched 12V
; for the purpose of switching on 12V to cs500

2:  Excitation with Delay (P22)
 1: 3        Ex Channel
 2: 0        Delay W/Ex (units = 0.01 sec)
 3: 10       Delay After Ex (units = 0.01 sec)
 4: 0        mV Excitation
; this only makes a delay, there is no excitation

3:  Volts (SE) (P1)
 1: 2        Reps
 2: 5        ñ 2500 mV Slow Range
 3: 3        SE Channel
 4: 21       Loc [ tair_1    ]
 5: .1       Mult
 6: -40      Offset
; temperature cs500 probe x 2
; this is a siungle ended measurement so it is InChan 3
; removed 60 Hz rejection range since there is no 60 Hz
; power tools creating electrical noise.

4:  Volts (SE) (P1)
 1: 2        Reps
 2: 5        ñ 2500 mV Slow Range
 3: 5        SE Channel
 4: 23       Loc [ rh_1      ]
 5: .1       Mult
 6: 0        Offset
; humidity cs500 probe x 2

5:  Do (P86)
 1: 53       Set Port 3 Low
; Voltage control port 3 turned off, switched 12V
; to the cs-500 is turned off.

6:  Pulse (P3)
 1: 2        Reps
 2: 1        Pulse Input Channel
 3: 2        Switch Closure
 4: 25       Loc [ U1        ]
 5: 0.08     Mult
 6: 0.447    Offset
; Met One Wind Speed -> m/s

7:  Excite-Delay (SE) (P4)
 1: 1        Reps
 2: 5        ñ 2500 mV Slow Range
 3: 7        SE Channel
 4: 3        Excite all reps w/Exchan 3
 5: 2        Delay (units 0.01 sec)
 6: 1000     mV Excitation
 7: 27       Loc [ U_dir1    ]
 8: 0.071    Mult
 9: 0.0      Offset
; Met One wind direction measurement.

; Following a loop of the barometer measurement that is
; performed for the last minute of each hour.
; The pressure sensor has a range from 600 - 1060 hPa,mb
; -> multiplier = sensor range (460)/mV Excitation(2500)
; = 0.184

8:  If time is (P92)
 1: 59       Minutes (Seconds --) into a
 2: 60       Interval (same units as above)
 3: 46       Set Port 6 High

9:  If time is (P92)
 1: 0        Minutes (Seconds --) into a
 2: 60       Interval (same units as above)
 3: 30       Then Do

10:  Volt (Diff) (P2)
 1: 1        Reps
 2: 5        ñ 2500 mV Slow Range
 3: 5        DIFF Channel
 4: 28       Loc [ pressure  ]
 5: .184     Mult
 6: 200      Offset
; CS105 Vaisala Barometer
; another 400 has to be added to pressure value to yield
; mb this can be done in post processing.

11:  Do (P86)
 1: 56       Set Port 6 Low

12:  End (P95)
; end pressure sensor measurement last minute of each hr 

; Following is the snow height measurement which
; occurs each 10 minutes, nested in a 15 second
; measurement interval.

13:  If time is (P92)
 1: 0        Minutes (Seconds --) into a
 2: 10       Interval (same units as above)
 3: 30       Then Do

14:  SDI-12 Recorder (OS10 1.1) (P105)
 1: 0     -- SDI-12 Address
 2: 0        Start Measurement (aM0!)
 3: 4        Port
 4: 29       Loc [ SD_1      ]
 5: 1        Mult
 6: 0        Offset
; sonic snow height sensor 1

15:  SDI-12 Recorder (OS10 1.1) (P105)
 1: 0        SDI-12 Address
 2: 0        Start Measurement (aM0!)
 3: 5        Port
 4: 30       Loc [ SD_2      ]
 5: 1        Mult
 6: 0        Offset
; sonic snow height sensor 2

16:  Batt Voltage (P10)
 1: 42       Loc [ BATTERY   ]

17:  End (P95)

*Table 3 Subroutines

End Program
















-Input Locations-
1 full_brg  0 1 0
2 net_rad   0 1 0
3 THR_up    0 1 0
4 THR_down  0 1 0
5 THR_res   0 1 0
6 THR_T_up  0 1 1
7 THR_T_do  0 1 0
8 L_in      0 0 1
9 tc1       0 1 0
10 tc2       0 1 0
11 tc3       0 1 0
12 tc4       0 1 0
13 tc5       0 1 0
14 tc6       0 1 0
15 tc7       0 1 0
16 tc8       0 1 0
17 tc9       0 1 0
18 tc10      0 1 0
19 tc_air1   0 0 0
20 tc_air2   0 0 0
21 tair_1    0 1 0
22 tair_2    0 1 0
23 rh_1      0 1 0
24 rh_2      0 1 0
25 U1        0 1 0
26 U2        0 1 0
27 U_dir1    4 1 1
28 pressure  0 1 1
29 SD_1      0 1 0
30 SD_2      0 2 0
35 TC_snow1  0 1 1
36 TC_snow2  0 1 0
37 TC_snow3  0 1 0
39 TC_snow4  0 1 0
40 TC_snow5  0 1 0
41 TC_snow6  0 1 0
42 BATTERY   0 1 1
43 Antenna   0 0 1
-Program Security-
0000
0000
0000
-Mode 4-
-Final Storage Area 2-
0
