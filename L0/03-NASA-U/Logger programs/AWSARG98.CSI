;{CR10}
;                                                version 03/20/1998
; --------------------------------------------------------------
; Program: awsarg98.csi (cr10x)
; to be loaded to aws installed in 1998.
; Program written by: Ted DeMaria; Jason Box; and Dan Anderson. 4-96
; modified for NASA-U (station 03, staion code 63) 26 May 2006
; --------------------------------------------------------------
; Program modified 03/20/1998 by Jason Box
; modifications:
; air and snow TC use 2.5 mV range if ref temp is greater than -35 C.
; air and snow TC use 7.5 mV range if ref temp is less than -35 C.
; SW radiation is set to zero if negative.
; replace peak wind speed with reference temperature.
; --------------------------------------------------------------
; This program measures various meteorological instruments
; and stores and transmits two blocks of data.  The first block
; contains what we consider to be the important measurements
; though both blocks are to be stored in hourly-mean format.
; So, we are attempting to transmit two differnt blocks of
; data stored in the two ARGOS buffers.
; --------------------------------------------------------------
; NOTE: this program is written for the cr10x not cr10
; The cr10 will take a program called awsarg96.doc
; --------------------------------------------------------------

*Table 1 Program
  01: 15.0      Execution Interval (seconds)
; 15 second suggested execution interval

1:  Set Port(s) (P20)
 1: 9999     C8,C7,C6,C5 Options
 2: 9933     C4,C3,C2,C1 Options
; Configure control ports so that 1 and 2 are
; milisecond pulses

2:  Do (P86)
 1: 42       Set Port 2 High
; send voltage from control port 2 to multiplexer
; to turn on multiplexer

3:  Full Bridge (P6)
 1: 1        Reps
 2: 22       ñ 7.5 mV 60 Hz Rejection Range
 3: 1        DIFF Channel
 4: 1        Excite all reps w/Exchan 1
 5: 350      mV Excitation
 6: 1        Loc [ full_brg  ]
 7: -.001    Mult
 8: .09707   Offset
; measures the output of the Full Bridge
; -> (internal multiplexer reference temperature signal)

4:  BR Transform Rf[X/(1-X)] (P59)
 1: 1        Reps
 2: 1        Loc [ full_brg  ]
 3: 10.025   Multiplier (Rf)
; Find the resistance of the reference temperature probe

5:  Temperature RTD (P16)
 1: 1        Reps
 2: 1        R/RO Loc [ full_brg  ]
 3: 1        Loc [ full_brg  ]
 4: 1        Mult
 5: 0.0000   Offset
; calculate the reference temperature

6:  Beginning of Loop (P87)
 1: 0        Delay
 2: 2        Loop Count
; begin multiplexer measurement loop

7:  Do (P86)
 1: 71       Pulse Port 1
; send voltage from control port 1 to multiplexer

8:  Do (P86)
 1: 71       Pulse Port 1
; same as above, must do 2 x to step forward one
; multiplexer input channel.

9:  Volt (Diff) (P2)
 1: 1        Reps
 2: 3        ñ 25 mV Slow Range
 3: 1        DIFF Channel
 4: 2     -- Loc [ sw_in     ]
 5: 1        Mult
 6: 0        Offset
; Two Li Cor Photo-Diode Pyranometers

10:  End (P95)
; end Li Cor multiplexer loop

11:  IF (X<=>F) (P89)
 1: 2        X Loc [ sw_in     ]
 2: 4        <
 3: 0.       F
 4: 30       Then Do

12:  Z=F (P30)
 1: 0        F
 2: 0        Exponent of 10
 3: 2        Z Loc [ sw_in     ]

13:  End (P95)
; end set negative SW in values to zero


14:  IF (X<=>F) (P89)
 1: 3        X Loc [ sw_ref    ]
 2: 4        <
 3: 0.       F
 4: 30       Then Do

15:  Z=F (P30)
 1: 0        F
 2: 0        Exponent of 10
 3: 3        Z Loc [ sw_ref    ]

16:  End (P95)
; end set negative SW ref values to zero

17:  Do (P86)
 1: 71       Pulse Port 1
; send voltage from control port 1 to multiplexer
; moves measurement to the next mplexer port

18:  Do (P86)
 1: 71       Pulse Port 1
; same as above, must do it twice

19:  Volt (Diff) (P2)
 1: 1        Reps
 2: 4        ñ 250 mV Slow Range
 3: 1        DIFF Channel
 4: 4     -- Loc [ net_rad   ]
 5: 1        Mult
 6: 0        Offset
; Net Radiometer

20:  Beginning of Loop (P87)
 1: 0        Delay
 2: 10       Loop Count
; begin thermocople multiplexer measurement loop

21:  Do (P86)
 1: 71       Pulse Port 1
; send voltage from control port 1 to multiplexer

22:  Do (P86)
 1: 71       Pulse Port 1
; same as above, must do it twice

23:  IF (X<=>F) (P89)
 1: 1        X Loc [ full_brg  ]
 2: 3        >=
 3: -35      F
 4: 30       Then Do

24:  Thermocouple Temp (DIFF) (P14)
 1: 1        Reps
 2: 01       ñ 2.5 mV Slow Range
 3: 1        DIFF Channel
 4: 1        Type T (Copper-Constantan)
 5: 1        Ref Temp Loc [ full_brg  ]
 6: 5     -- Loc [ tc1       ]
 7: 1        Mult
 8: 0        Offset
; Sub-surface Ice Thermocouple string (0.1 m .. 9.1 m initial)

25:  End (P95)
; end if statement for T > -35 C.

26:  IF (X<=>F) (P89)
 1: 1        X Loc [ full_brg  ]
 2: 4        <
 3: -35      F
 4: 30       Then Do

27:  Thermocouple Temp (DIFF) (P14)
 1: 1        Reps
 2: 2        ñ 7.5 mV Slow Range
 3: 1        DIFF Channel
 4: 1        Type T (Copper-Constantan)
 5: 1        Ref Temp Loc [ full_brg  ]
 6: 5     -- Loc [ tc1       ]
 7: 1        Mult
 8: 0        Offset
; Sub-surface Ice Thermocouple string (0.1 m .. 9.1 m initial)

28:  End (P95)
; end if statement for T < -35 C.

29:  End (P95)
; end loop of 10, thermocouple measurement loop.

30:  Beginning of Loop (P87)
 1: 0        Delay
 2: 2        Loop Count
; begin air temperature thermocouple multiplexer loop

31:  Do (P86)
 1: 71       Pulse Port 1

32:  Do (P86)
 1: 71       Pulse Port 1

33:  IF (X<=>F) (P89)
 1: 1        X Loc [ full_brg  ]
 2: 3        >=
 3: -35      F
 4: 30       Then Do

34:  Thermocouple Temp (DIFF) (P14)
 1: 1        Reps
 2: 01       ñ 2.5 mV Slow Range
 3: 1        DIFF Channel
 4: 2        Type E (Chromel-Constantan)
 5: 1        Ref Temp Loc [ full_brg  ]
 6: 15    -- Loc [ tc_air1   ]
 7: 1        Mult
 8: 0        Offset
; 2 x temperature mesurement

35:  End (P95)
; end if statement, T > -30 C

36:  IF (X<=>F) (P89)
 1: 1        X Loc [ full_brg  ]
 2: 4        <
 3: -35      F
 4: 30       Then Do

37:  Thermocouple Temp (DIFF) (P14)
 1: 1        Reps
 2: 2        ñ 7.5 mV Slow Range
 3: 1        DIFF Channel
 4: 2        Type E (Chromel-Constantan)
 5: 1        Ref Temp Loc [ full_brg  ]
 6: 15    -- Loc [ tc_air1   ]
 7: 1        Mult
 8: 0        Offset
; 2 x temperature mesurement


38:  End (P95)
; end if statement, T < -30 C

39:  End (P95)
; end air temperature thermocouple multiplexer loop

40:  Do (P86)
 1: 52       Set Port 2 Low
; turns multiplexer off, the remaining measurements
; will occur on the cr10 and not on the multiplexer.

41:  If time is (P92)
 1: 00       Minutes (Seconds --) into a
 2: 60       Interval (same units as above)
 3: 10       Set Output Flag High
; send data to final storage each hour
; 14 parameters are to be measured and averaged,
; 14 + JD and time = 16, the maximum
; number of parameters ARGOS will transmit.

42:  Set Active Storage Area (P80)^16284
 1: 01       Final Storage Area 1
 2: 032      Array ID or Loc [ _________ ]
; set station array id number, 01 = swiss camp,
; 02 = crawford point, ... in the order that they
; were installed.  So, field season 1996, first AWS
; will be number 06.
; set array id = station number plus block number
; array id # = station number (06) + block number (2) -> id#=062

43:  Real Time (P77)^7467
 1: 0110     Day,Hour/Minute

44:  Average (P71)^12683
 1: 3        Reps
 2: 2        Loc [ sw_in     ]
; sw in, sw ref, net, loc 2-4

45:  Average (P71)^10685
 1: 2        Reps
 2: 15       Loc [ tc_air1   ]
; 2 x air temperature thermocouple averages, loc 15-16

46:  Average (P71)^7829
 1: 4        Reps
 2: 19       Loc [ rh_1      ]
; 2 x cs-500 relative humidity averages, loc 19-20
; 2 x wind speed averages, loc 21 - 22

47:  Average (P71)^21405
 1: 2        Reps
 2: 23       Loc [ U_dir1    ]
; 1 x wind direction average from the higher wind monitor, loc 24

48:  Sample (P70)^978
 1: 1        Reps
 2: 25       Loc [ pressure  ]

49:  Average (P71)^31545
 1: 2        Reps
 2: 26       Loc [ SD_1      ]
; 2 x snow height averages, loc 26-27

50:  If time is (P92)
 1: 00       Minutes (Seconds --) into a
 2: 60       Interval (same units as above)
 3: 30       Then Do

51:  Data Transfer to ARGOS ST-13 (P121)
 1: 23       Command Byte #1
 2: 0000     Command Byte #2
 3: 0000     Command Byte #3

52:  End (P95)

53:  If time is (P92)
 1: 0        Minutes (Seconds --) into a
 2: 60       Interval (same units as above)
 3: 10       Set Output Flag High

54:  Set Active Storage Area (P80)^574
 1: 1        Final Storage Area 1
 2: 032      Array ID or Loc [ _________ ]
; set array id = station number plus block number
; array id # = station number (06) + block number (2) -> id#=062

55:  Real Time (P77)^29915
 1: 0110     Day,Hour/Minute

56:  Average (P71)^6852
 1: 10       Reps
 2: 5        Loc [ tc1       ]
; snow thermocouple measurements tc1 - tc10

57:  Average (P71)^32620
 1: 2        Reps
 2: 17       Loc [ tair_1    ]
; CS-500 air temperature measurements 1 and 2

58:  Average (P71)^8627
 1: 1        Reps
 2: 1        Loc [ full_brg  ]

59:  Sample (P70)^18522
 1: 1        Reps
 2: 28       Loc [ BATTERY   ]

60:  If time is (P92)
 1: 0        Minutes (Seconds --) into a
 2: 60       Interval (same units as above)
 3: 30       Then Do

61:  Data Transfer to ARGOS ST-13 (P121)
 1: 39       Command Byte #1
 2: 0000     Command Byte #2
 3: 0000     Command Byte #3
; selects second memory buffer.

62:  Data Transfer to ARGOS ST-13 (P121)
 1: 118      Command Byte #1
 2: 0000     Command Byte #2
 3: 0000     Command Byte #3
; toggles transmitter memory buffer between 1 and 2

63:  End (P95)
; end second buffer transmission

*Table 2 Program
  02: 60.0      Execution Interval (seconds)
; 60 second suggested sampling interval
; in 1995 this was 600, but filled memory with wind
; sensor pulses.
; if we set exec to 60 sec, we will not overfill the
; memory buffer, yet we can still measure certain sensors
; on a 600 sec interval.

1:  Do (P86)
 1: 43       Set Port 3 High
; voltage control port 3, jumped to switched 12V
; for the purpose of switching on 12V to cs500

2:  Excitation with Delay (P22)
 1: 3        Ex Channel
 2: 0        Delay W/Ex (units = 0.01 sec)
 3: 10       Delay After Ex (units = 0.01 sec)
 4: 0        mV Excitation
; this only makes a delay, there is no excitation

3:  Volts (SE) (P1)
 1: 2        Reps
 2: 5        ñ 2500 mV Slow Range
 3: 3        SE Channel
 4: 17       Loc [ tair_1    ]
 5: .1       Mult
 6: -40      Offset
; temperature cs500 probe x 2
; this is a siungle ended measurement so it is InChan 3
; removed 60 Hz rejection range since there is no 60 Hz
; power tools creating electrical noise.

4:  Volts (SE) (P1)
 1: 2        Reps
 2: 5        ñ 2500 mV Slow Range
 3: 5        SE Channel
 4: 19       Loc [ rh_1      ]
 5: .1       Mult
 6: 0        Offset
; humidity cs500 probe x 2

5:  Do (P86)
 1: 53       Set Port 3 Low
; Voltage control port 3 turned off, switched 12V
; to the cs-500 is turned off.

6:  Pulse (P3)
 1: 2        Reps
 2: 1        Pulse Input Channel
 3: 21       Low Level AC, Output Hz
 4: 21       Loc [ U1        ]
 5: 0.0980   Mult
 6: 0        Offset
; Young Wind Monitor wind speed -> m/s

7:  Excite-Delay (SE) (P4)
 1: 2        Reps
 2: 5        ñ 2500 mV Slow Range
 3: 7        SE Channel
 4: 2        Excite all reps w/Exchan 2
 5: 2        Delay (units 0.01 sec)
 6: 2500     mV Excitation
 7: 23       Loc [ U_dir1    ]
 8: 0.142    Mult
 9: 0        Offset
; Young wind direction measurement.

; Following a loop of the barometer measurement that is
; performed for the last minute of each hour.
; The pressure sensor has a range from 600 - 1060 hPa,mb
; -> multiplier = sensor range (460)/mV Excitation(2500)
; = 0.184

8:  If time is (P92)
 1: 59       Minutes (Seconds --) into a
 2: 60       Interval (same units as above)
 3: 46       Set Port 6 High

9:  If time is (P92)
 1: 0        Minutes (Seconds --) into a
 2: 60       Interval (same units as above)
 3: 30       Then Do

10:  Volts (SE) (P1)
 1: 1        Reps
 2: 5        ñ 2500 mV Slow Range
 3: 9        SE Channel
 4: 25       Loc [ pressure  ]
 5: .184     Mult
 6: 200      Offset
; CS105 Vaisala Barometer
; another 400 has to be added to pressure value to yield
; mb this can be done in post processing.

11:  Do (P86)
 1: 56       Set Port 6 Low

12:  Batt Voltage (P10)
 1: 28       Loc [ BATTERY   ]

13:  End (P95)
; end pressure sensor measurement last minute of each hr

; Following is the snow height measurement which
; occurs each 10 minutes, nested in a 15 second
; measurement interval.

14:  If time is (P92)
 1: 0        Minutes (Seconds --) into a
 2: 10       Interval (same units as above)
 3: 30       Then Do

15:  SDI-12 Recorder (OS10 1.1) (P105)
 1: 0        SDI-12 Address
 2: 0        Start Measurement (aM0!)
 3: 4        Port
 4: 26       Loc [ SD_1      ]
 5: 1        Mult
 6: 0        Offset
; sonic snow height sensor 1

16:  SDI-12 Recorder (OS10 1.1) (P105)
 1: 0        SDI-12 Address
 2: 0        Start Measurement (aM0!)
 3: 5        Port
 4: 27       Loc [ SD_2      ]
 5: 1        Mult
 6: 0        Offset
; sonic snow height sensor 2

17:  End (P95)

*Table 3 Subroutines

End Program















-Input Locations-
1 full_brg  0 17 0
2 sw_in     0 1 1
3 sw_ref    0 1 1
4 net_rad   0 0 0
5 tc1       4 1 2
6 tc2       0 1 0
7 tc3       0 1 0
8 tc4       0 1 0
9 tc5       0 1 0
10 tc6       0 1 0
11 tc7       0 1 0
12 tc8       0 1 0
13 tc9       0 1 0
14 tc10      0 1 0
15 tc_snowa  4 1 2
16 tc_snowb  4 1 2
17 tc_air1   4 0 3
18 tc_air2   0 0 0
19 tair_1    0 1 0
20 tair_2    0 1 0
21 rh_1      0 1 0
22 rh_2      0 1 0
23 U1        0 1 0
24 U2        0 1 0
25 U_dir1    4 1 1
26 U_dir2    16 2 1
27 pressure  0 2 1
28 SD_1      0 2 0
29 SD_2      0 2 0
30 BATTERY   0 2 2
31 thr_top   4 1 0
32 thr_bot   0 1 1
33 thr_net   0 1 0
34 thr_res   0 1 1
35 thr_tctop 0 1 1
36 thr_tcbot 0 1 0
37 powr      0 1 1
-Program Security-
0000
0000
0000
-Mode 4-
-Final Storage Area 2-
0
-DLD File Labels-
0
-Final Storage Labels-
0,32,16284
1,Day_RTM,7467
1,Hour_Minute_RTM
2,sw_in_AVG~2,12683
2,sw_ref_AVG~3
2,net_rad_AVG~4
3,tc_snowa_AVG~15,10685
3,tc_snowb_AVG~16
4,tair_1_AVG~19,7829
4,tair_2_AVG~20
4,rh_1_AVG~21
4,rh_2_AVG~22
5,U1_AVG~23,21405
5,U2_AVG~24
6,U_dir1~25,978
7,U_dir2_AVG~26,31545
7,pressure_AVG~27
8,32,574
9,Day_RTM,29915
9,Hour_Minute_RTM
10,tc1_AVG~5,6852
10,tc2_AVG~6
10,tc3_AVG~7
10,tc4_AVG~8
10,tc5_AVG~9
10,tc6_AVG~10
10,tc7_AVG~11
10,tc8_AVG~12
10,tc9_AVG~13
10,tc10_AVG~14
11,tc_air1_AVG~17,32620
11,tc_air2_AVG~18
12,full_brg_AVG~1,8627
13,SD_1~28,18522
