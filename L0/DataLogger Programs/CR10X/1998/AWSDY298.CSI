;{CR10}
; --------------------------------------------------------------
; Program: awsdy298.csi (cr10x)
; Program written by: Ted DeMaria; Jason Box; and Dan Anderson. 4-96
; --------------------------------------------------------------
; Program modified 03/20/1998 by Jason Box
; modifications:
; air and snow TC use 2.5 mV range if ref temp is greater than -35 C.
; air and snow TC use 7.5 mV range if ref temp is less than -35 C.
; SW radiation is set to zero if negative.
; --------------------------------------------------------------
; This program measures various meteorological instruments
; NOTE: this program is written for the cr10x not cr10
; The cr10 will take a program called awsarg96.doc
; --------------------------------------------------------------

*Table 1 Program
  01: 15.0      Execution Interval (seconds)
; 15 second suggested execution interval

1:  Set Port(s) (P20)
 1: 9999     C8,C7,C6,C5 Options
 2: 9933     C4,C3,C2,C1 Options
; Configure control ports so that 1 and 2 are
; milisecond pulses

2:  Do (P86)
 1: 42       Set Port 2 High
; send voltage from control port 2 to multiplexer
; to turn on multiplexer

3:  Full Bridge (P6)
 1: 1        Reps
 2: 22       ñ 7.5 mV 60 Hz Rejection Range
 3: 1        DIFF Channel
 4: 1        Excite all reps w/Exchan 1
 5: 350      mV Excitation
 6: 1        Loc [ full_brg  ]
 7: -.001    Mult
 8: .09707   Offset
; measures the output of the Full Bridge
; -> (internal multiplexer reference temperature signal)

4:  BR Transform Rf[X/(1-X)] (P59)
 1: 1        Reps
 2: 1        Loc [ full_brg  ]
 3: 10.025   Multiplier (Rf)
; Find the resistance of the reference temperature probe

5:  Temperature RTD (P16)
 1: 1        Reps
 2: 1        R/RO Loc [ full_brg  ]
 3: 1        Loc [ full_brg  ]
 4: 1        Mult
 5: 0.0000   Offset
; calculate the reference temperature

6:  Beginning of Loop (P87)
 1: 0        Delay
 2: 2        Loop Count
; begin multiplexer measurement loop

7:  Do (P86)
 1: 71       Pulse Port 1
; send voltage from control port 1 to multiplexer

8:  Do (P86)
 1: 71       Pulse Port 1
; same as above, must do 2 x to step forward one
; multiplexer input channel.

9:  Volt (Diff) (P2)
 1: 1        Reps
 2: 3        ñ 25 mV Slow Range
 3: 1        DIFF Channel
 4: 2     -- Loc [ sw_in     ]
 5: 1        Mult
 6: 0        Offset
; Two Li Cor Photo-Diode Pyranometers

10:  End (P95)
; end Li Cor multiplexer loop

11:  IF (X<=>F) (P89)
 1: 2        X Loc [ sw_in     ]
 2: 4        <
 3: 0.       F
 4: 30       Then Do

12:  Z=F (P30)
 1: 0        F
 2: 0        Exponent of 10
 3: 2        Z Loc [ sw_in     ]

13:  End (P95)
; end set negative SW in values to zero


14:  IF (X<=>F) (P89)
 1: 3        X Loc [ sw_ref    ]
 2: 4        <
 3: 0.       F
 4: 30       Then Do

15:  Z=F (P30)
 1: 0        F
 2: 0        Exponent of 10
 3: 3        Z Loc [ sw_ref    ]

16:  End (P95)
; end set negative SW ref values to zero

17:  Do (P86)
 1: 71       Pulse Port 1
; send voltage from control port 1 to multiplexer
; moves measurement to the next mplexer port

18:  Do (P86)
 1: 71       Pulse Port 1
; same as above, must do it twice

19:  Volt (Diff) (P2)
 1: 1        Reps
 2: 4        ñ 250 mV Slow Range
 3: 1        DIFF Channel
 4: 4     -- Loc [ net_rad   ]
 5: 1        Mult
 6: 0        Offset
; Net Radiometer

20:  Beginning of Loop (P87)
 1: 0        Delay
 2: 10       Loop Count
; begin thermocople multiplexer measurement loop

21:  Do (P86)
 1: 71       Pulse Port 1
; send voltage from control port 1 to multiplexer

22:  Do (P86)
 1: 71       Pulse Port 1
; same as above, must do it twice

23:  IF (X<=>F) (P89)
 1: 1        X Loc [ full_brg  ]
 2: 3        >=
 3: -35      F
 4: 30       Then Do

24:  Thermocouple Temp (DIFF) (P14)
 1: 1        Reps
 2: 01       ñ 2.5 mV Slow Range
 3: 1        DIFF Channel
 4: 1        Type T (Copper-Constantan)
 5: 1        Ref Temp Loc [ full_brg  ]
 6: 5     -- Loc [ tc1       ]
 7: 1        Mult
 8: 0        Offset
; Sub-surface Ice Thermocouple string (0.1 m .. 9.1 m initial)

25:  End (P95)
; end if statement for T > -35 C.
   
26:  IF (X<=>F) (P89)
 1: 1        X Loc [ full_brg  ]
 2: 4        <
 3: -35      F
 4: 30       Then Do

27:  Thermocouple Temp (DIFF) (P14)
 1: 1        Reps
 2: 2        ñ 7.5 mV Slow Range
 3: 1        DIFF Channel
 4: 1        Type T (Copper-Constantan)
 5: 1        Ref Temp Loc [ full_brg  ]
 6: 5     -- Loc [ tc1       ]
 7: 1        Mult
 8: 0        Offset
; Sub-surface Ice Thermocouple string (0.1 m .. 9.1 m initial)

28:  End (P95)
; end if statement for T < -35 C.

29:  End (P95)
; end loop of 10, thermocouple measurement loop.

30:  Beginning of Loop (P87)
 1: 0        Delay
 2: 2        Loop Count
; begin air temperature thermocouple multiplexer loop

31:  Do (P86)
 1: 71       Pulse Port 1

32:  Do (P86)
 1: 71       Pulse Port 1

33:  IF (X<=>F) (P89)
 1: 1        X Loc [ full_brg  ]
 2: 3        >=
 3: -35      F
 4: 30       Then Do

34:  Thermocouple Temp (DIFF) (P14)
 1: 1        Reps
 2: 01       ñ 2.5 mV Slow Range
 3: 1        DIFF Channel
 4: 2        Type E (Chromel-Constantan)
 5: 1        Ref Temp Loc [ full_brg  ]
 6: 17    -- Loc [ tc_air1   ]
 7: 1        Mult
 8: 0        Offset
; 2 x temperature mesurement 

35:  End (P95)
; end if statement, T > -30 C

36:  IF (X<=>F) (P89)
 1: 1        X Loc [ full_brg  ]
 2: 4        <
 3: -35      F
 4: 30       Then Do

37:  Thermocouple Temp (DIFF) (P14)
 1: 1        Reps
 2: 2        ñ 7.5 mV Slow Range
 3: 1        DIFF Channel
 4: 2        Type E (Chromel-Constantan)
 5: 1        Ref Temp Loc [ full_brg  ]
 6: 17    -- Loc [ tc_air1   ]
 7: 1        Mult
 8: 0        Offset
; 2 x temperature mesurement 


38:  End (P95)
; end if statement, T < -30 C

39:  End (P95)
; end air temperature thermocouple multiplexer loop

40:  Beginning of Loop (P87)
 1: 0        Delay
 2: 3        Loop Count
; begin thr thermopile loop

41:  Do (P86)
 1: 71       Pulse Port 1

42:  Do (P86)
 1: 71       Pulse Port 1

43:  Volt (Diff) (P2)
 1: 1        Reps
 2: 4        ñ 250 mV Slow Range
 3: 1        DIFF Channel
 4: 31    -- Loc [ thr_top   ]
 5: 1.0      Mult
 6: 0.0      Offset

; 3 x thermopile measurment

44:  End (P95)
; end air temperature thermocouple multiplexer loop


45:  Do (P86)
 1: 71       Pulse Port 1

46:  Do (P86)
 1: 71       Pulse Port 1

47:  Do (P86)
 1: 47       Set Port 7 High

48:  Volt (Diff) (P2)
 1: 1        Reps
 2: 5        ñ 2500 mV Slow Range
 3: 1        DIFF Channel
 4: 34    -- Loc [ thr_res   ]
 5: 1        Mult
 6: 0        Offset

49:  Do (P86)
 1: 57       Set Port 7 Low

50:  Beginning of Loop (P87)
 1: 0        Delay
 2: 2        Loop Count
; begin thr thermocouple top an bottom

51:  Do (P86)
 1: 71       Pulse Port 1

52:  Do (P86)
 1: 71       Pulse Port 1

53:  Volt (Diff) (P2)
 1: 1        Reps
 2: 1        ñ 2.5 mV Slow Range
 3: 1        DIFF Channel
 4: 35    -- Loc [ thr_tctop ]
 5: 1.0      Mult
 6: 0.0      Offset

54:  End (P95)


55:  Do (P86)
 1: 71       Pulse Port 1

56:  Do (P86)
 1: 71       Pulse Port 1

57:  IF (X<=>F) (P89)
 1: 1        X Loc [ full_brg  ]
 2: 3        >=
 3: -35      F
 4: 30       Then Do

58:  Thermocouple Temp (DIFF) (P14)
 1: 1        Reps
 2: 01       ñ 2.5 mV Slow Range
 3: 1        DIFF Channel
 4: 2        Type E (Chromel-Constantan)
 5: 1        Ref Temp Loc [ full_brg  ]
 6: 15    -- Loc [ tc_snowa  ]
 7: 1        Mult
 8: 0        Offset
; 2 x temperature mesurement 

59:  End (P95)
; end if Reff T >= -35

60:  IF (X<=>F) (P89)
 1: 1        X Loc [ full_brg  ]
 2: 4        <
 3: -35      F
 4: 30       Then Do

61:  Thermocouple Temp (DIFF) (P14)
 1: 1        Reps
 2: 2        ñ 7.5 mV Slow Range
 3: 1        DIFF Channel
 4: 2        Type E (Chromel-Constantan)
 5: 1        Ref Temp Loc [ full_brg  ]
 6: 15    -- Loc [ tc_snowa  ]
 7: 1        Mult
 8: 0        Offset
; 2 x temperature mesurement 

62:  End (P95)
; end if Reff T < -35

63:  Do (P86)
 1: 71       Pulse Port 1

64:  Do (P86)
 1: 71       Pulse Port 1

65:  IF (X<=>F) (P89)
 1: 1        X Loc [ full_brg  ]
 2: 3        >=
 3: -35      F
 4: 30       Then Do

66:  Thermocouple Temp (DIFF) (P14)
 1: 1        Reps
 2: 01       ñ 2.5 mV Slow Range
 3: 1        DIFF Channel
 4: 1        Type T (Copper-Constantan)
 5: 1        Ref Temp Loc [ full_brg  ]
 6: 16    -- Loc [ tc_snowb  ]
 7: 1        Mult
 8: 0        Offset

67:  End (P95)
; end if Reff T >= -35 C.

68:  IF (X<=>F) (P89)
 1: 1        X Loc [ full_brg  ]
 2: 4        <
 3: -35      F
 4: 30       Then Do

69:  Thermocouple Temp (DIFF) (P14)
 1: 1        Reps
 2: 2        ñ 7.5 mV Slow Range
 3: 1        DIFF Channel
 4: 1        Type T (Copper-Constantan)
 5: 1        Ref Temp Loc [ full_brg  ]
 6: 16    -- Loc [ tc_snowb  ]
 7: 1        Mult
 8: 0        Offset

70:  End (P95)
; end if Reff T < -35 C.

71:  Do (P86)
 1: 52       Set Port 2 Low
; turns multiplexer off, the remaining measurements
; will occur on the cr10 and not on the multiplexer.

72:  If time is (P92)
 1: 00       Minutes (Seconds --) into a
 2: 60       Interval (same units as above)
 3: 10       Set Output Flag High
; send data to final storage each hour 
; 14 parameters are to be measured and averaged,
; 14 + JD and time = 16, the maximum
; number of parameters ARGOS will transmit.

73:  Set Active Storage Area (P80)
 1: 01       Final Storage Area 1
 2: 108      Array ID or Loc [ _________ ]
; set station array id number, 01 = swiss camp,
; 02 = crawford point, ... in the order that they
; were installed.  So, field season 1996, first AWS
; will be number 06.
; set array id = station number plus block number
; array id # = station number (06) + block number (2) -> id#=062

; List of AWS ID Codes
; ---------------------
; Swiss Camp   = AWS 01
; Crawford Pt  = AWS 02
; NASA-U       = AWS 03
; GITS, CAMP C = AWS 04
; Humboldt     = AWS 05

74:  Real Time (P77)
 1: 0110     Day,Hour/Minute

75:  Average (P71)
 1: 3        Reps
 2: 2        Loc [ sw_in     ]
; sw in, sw ref, net, loc 2-4

76:  Average (P71)
 1: 2        Reps
 2: 17       Loc [ tc_air1   ]
; 2 x air temperature thermocouple averages, loc 15-16

77:  Average (P71)
 1: 6        Reps
 2: 21       Loc [ rh_1      ]
; 2 x cs-500 relative humidity averages, loc 19-20
; 2 x wind speed averages
; 2 x wind direction average 

78:  Sample (P70)
 1: 1        Reps
 2: 27       Loc [ pressure  ]

79:  Average (P71)
 1: 2        Reps
 2: 28       Loc [ SD_1      ]
; 2 x snow height

80:  Average (P71)
 1: 10       Reps
 2: 5        Loc [ tc1       ]
; snow thermocouple measurements tc1 - tc10

81:  Average (P71)
 1: 2        Reps
 2: 15       Loc [ tc_snowa  ]

82:  Average (P71)
 1: 2        Reps
 2: 19       Loc [ tair_1    ]
; CS-500 air temperature measurements 1 and 2

83:  Sample (P70)
 1: 1        Reps
 2: 30       Loc [ BATTERY   ]

84:  Average (P71)
 1: 6        Reps
 2: 31       Loc [ thr_top   ]

85:  Sample (P70)
 1: 1        Reps
 2: 37       Loc [ powr      ]

86:  If time is (P92)
 1: 28       Minutes (Seconds --) into a
 2: 60       Interval (same units as above)
 3: 30       Then Do

87:  Data Transfer to GOES (P120)
 1: 0        self-timed buffer/append new data to old data
 2: 37       FWD/Ref Power Loc [ powr      ]

88:  End (P95)
; end second buffer transmission

*Table 2 Program
  02: 60.0      Execution Interval (seconds)
; 60 second suggested sampling interval
; in 1995 this was 600, but filled memory with wind
; sensor pulses.
; if we set exec to 60 sec, we will not overfill the 
; memory buffer, yet we can still measure certain sensors
; on a 600 sec interval.

1:  Do (P86)
 1: 43       Set Port 3 High
; voltage control port 3, jumped to switched 12V
; for the purpose of switching on 12V to cs500

2:  Excitation with Delay (P22)
 1: 3        Ex Channel
 2: 0        Delay W/Ex (units = 0.01 sec)
 3: 10       Delay After Ex (units = 0.01 sec)
 4: 0        mV Excitation
; this only makes a delay, there is no excitation

3:  Volts (SE) (P1)
 1: 2        Reps
 2: 5        ñ 2500 mV Slow Range
 3: 3        SE Channel
 4: 19       Loc [ tair_1    ]
 5: .1       Mult
 6: -40      Offset
; temperature cs500 probe x 2
; this is a siungle ended measurement so it is InChan 3

4:  Volts (SE) (P1)
 1: 2        Reps
 2: 5        ñ 2500 mV Slow Range
 3: 5        SE Channel
 4: 21       Loc [ rh_1      ]
 5: .1       Mult
 6: 0        Offset
; humidity cs500 probe x 2

5:  Do (P86)
 1: 53       Set Port 3 Low
; Voltage control port 3 turned off, switched 12V
; to the cs-500 is turned off.

6:  Pulse (P3)
 1: 2        Reps
 2: 1        Pulse Input Channel
 3: 21       Low Level AC, Output Hz
 4: 23       Loc [ U1        ]
 5: 0.0980   Mult
 6: 0        Offset
; Young Wind Monitor wind speed -> m/s

7:  Excite-Delay (SE) (P4)
 1: 2        Reps
 2: 5        ñ 2500 mV Slow Range
 3: 7        SE Channel
 4: 2        Excite all reps w/Exchan 2
 5: 2        Delay (units 0.01 sec)
 6: 2500     mV Excitation
 7: 25       Loc [ U_dir1    ]
 8: 0.142    Mult
 9: 0        Offset
; Young wind direction measurement.

; Following a loop of the barometer measurement that is
; performed for the last minute of each hour.
; The pressure sensor has a range from 600 - 1060 hPa,mb
; -> multiplier = sensor range (460)/mV Excitation(2500)
; = 0.184

8:  If time is (P92)
 1: 59       Minutes (Seconds --) into a
 2: 60       Interval (same units as above)
 3: 46       Set Port 6 High

9:  If time is (P92)
 1: 0        Minutes (Seconds --) into a
 2: 60       Interval (same units as above)
 3: 30       Then Do

10:  Volts (SE) (P1)
 1: 1        Reps
 2: 5        ñ 2500 mV Slow Range
 3: 9        SE Channel
 4: 27       Loc [ pressure  ]
 5: .184     Mult
 6: 200      Offset
; CS105 Vaisala Barometer
; another 400 has to be added to pressure value to yield
; mb this can be done in post processing.

11:  Do (P86)
 1: 56       Set Port 6 Low

12:  Batt Voltage (P10)
 1: 30       Loc [ BATTERY   ]

13:  End (P95)
; end pressure sensor measurement last minute of each hr 

; Following is the snow height measurement which
; occurs each 10 minutes, nested in a 15 second
; measurement interval.

14:  If time is (P92)
 1: 0        Minutes (Seconds --) into a
 2: 10       Interval (same units as above)
 3: 30       Then Do

15:  SDI-12 Recorder (OS10 1.1) (P105)
 1: 0        SDI-12 Address
 2: 0        Start Measurement (aM0!)
 3: 4        Port
 4: 28       Loc [ SD_1      ]
 5: 1        Mult
 6: 0        Offset
; sonic snow height sensor 1

16:  SDI-12 Recorder (OS10 1.1) (P105)
 1: 0        SDI-12 Address
 2: 0        Start Measurement (aM0!)
 3: 5        Port
 4: 29       Loc [ SD_2      ]
 5: 1        Mult
 6: 0        Offset
; sonic snow height sensor 2

17:  End (P95)

*Table 3 Subroutines

End Program























-Input Locations-
1 full_brg  0 17 0
2 sw_in     0 1 1
3 sw_ref    0 1 1
4 net_rad   0 0 0
5 tc1       4 1 2
6 tc2       0 1 0
7 tc3       0 1 0
8 tc4       0 1 0
9 tc5       0 1 0
10 tc6       0 1 0
11 tc7       0 1 0
12 tc8       0 1 0
13 tc9       0 1 0
14 tc10      0 1 0
15 tc_snowa  4 1 2
16 tc_snowb  4 1 2
17 tc_air1   4 0 3
18 tc_air2   0 0 0
19 tair_1    0 1 0
20 tair_2    0 1 0
21 rh_1      0 1 0
22 rh_2      0 1 0
23 U1        0 1 0
24 U2        0 1 0
25 U_dir1    4 1 1
26 U_dir2    16 2 1
27 pressure  0 2 1
28 SD_1      0 2 0
29 SD_2      0 2 0
30 BATTERY   0 2 2
31 thr_top   4 1 0
32 thr_bot   0 1 1
33 thr_net   0 1 0
34 thr_res   0 1 1
35 thr_tctop 0 1 1
36 thr_tcbot 0 1 0
37 powr      0 1 1
-Program Security-
0000
0000
0000
-Mode 4-
-Final Storage Area 2-
0
