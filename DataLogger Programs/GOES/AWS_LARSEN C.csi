;{CR10X}
;
;_______________________________________________________________________________
; Larsen C Prject: with new TX312 GOES transmitter
;
; Modified Koni Steffen, Rothera 6 Nov 2008
;
; excitation changed from 350 mV to 250 to extend measurement range to -72 C
; application of resistance to temp conversion also changed.  Modifications suggested
; by Ed Swaitec March 2000
; delay increased from 0.1 to 1 sec on T/RH
;________________________________________________________________________________
;
*Table 1 Program
  01: 15        Execution Interval (seconds)
; 15 second suggested execution interval

1:  Set Port(s) (P20)
 1: 8979     C8..C5 = input/nc/output/nc
 2: 9933     C4,C3,C2,C1 Options
; Configure control ports so that 1 and 2 are
; milisecond pulses

2:  Do (P86)
 1: 42       Set Port 2 High
; send voltage from control port 2 to multiplexer
; to turn on multiplexer

3:  Full Bridge (P6)
 1: 1        Reps
 2: 2        7.5 mV Slow Range
 3: 1        In Chan
 4: 1        Excite all reps w/Exchan 1
 5: 250      mV Excitation
 6: 1        Loc [ ref_temp  ]
 7: -.001    Mult
 8: .09707   Offset
;Compute resistance.

4:  BR Transform Rf[X/(1-X)] (P59)
 1: 1        Reps
 2: 1        Loc [ ref_temp  ]
 3: 10.025   Multiplier (Rf)
; Find the resistance of the reference temperature probe

;T = (Rs-1)/alpha1
;
5:  Z=X+F (P34)
 1: 1        X Loc [ ref_temp  ]
 2: -1       F
 3: 1        Z Loc [ ref_temp  ]

6:  Z=F (P30)
 1: .00385   F
 2: 0        Exponent of 10
 3: 33       Z Loc [ alpha1    ]

7:  Z=X/Y (P38)
 1: 1        X Loc [ ref_temp  ]
 2: 33       Y Loc [ alpha1    ]
 3: 1        Z Loc [ ref_temp  ]

8:  Beginning of Loop (P87)
 1: 0        Delay
 2: 2        Loop Count
; begin multiplexer measurement loop

9:  Do (P86)
 1: 71       Pulse Port 1
; send voltage from control port 1 to multiplexer

10:  Do (P86)
 1: 71       Pulse Port 1
; same as above, must do 2 x to step forward one
; multiplexer input channel.

11:  Volt (Diff) (P2)
 1: 1        Reps
 2: 3        ñ 25 mV Slow Range
 3: 1        DIFF Channel
 4: 2     -- Loc [ sw_in     ]
 5: 1        Mult
 6: 0        Offset
; Two Li Cor Photo-Diode Pyranometers

12:  End (P95)
; end Li Cor multiplexer loop

13:  IF (X<=>F) (P89)
 1: 2        X Loc [ sw_in     ]
 2: 4        <
 3: 0.       F
 4: 30       Then Do

14:  Z=F (P30)
 1: 0        F
 2: 0        Exponent of 10
 3: 2        Z Loc [ sw_in     ]

15:  End (P95)
; end set negative SW in values to zero

16:  IF (X<=>F) (P89)
 1: 3        X Loc [ sw_ref    ]
 2: 4        <
 3: 0.       F
 4: 30       Then Do

17:  Z=F (P30)
 1: 0        F
 2: 0        Exponent of 10
 3: 3        Z Loc [ sw_ref    ]

18:  End (P95)
; end set negative SW ref values to zero

19:  Do (P86)
 1: 71       Pulse Port 1
; send voltage from control port 1 to multiplexer
; moves measurement to the next mplexer port

20:  Do (P86)
 1: 71       Pulse Port 1
; same as above, must do it twice

21:  Volt (Diff) (P2)
 1: 1        Reps
 2: 4        ñ 250 mV Slow Range
 3: 1        DIFF Channel
 4: 4     -- Loc [ net_rad   ]
 5: 1        Mult
 6: 0        Offset
; Net Radiometer

; ---------------------- begin snow TC loop
22:  Beginning of Loop (P87)
 1: 0        Delay
 2: 10       Loop Count
; begin thermocople multiplexer measurement loop

23:  Do (P86)
 1: 71       Pulse Port 1
; send voltage from control port 1 to multiplexer

24:  Do (P86)
 1: 71       Pulse Port 1
; same as above, must do it twice

25:  Thermocouple Temp (DIFF) (P14)
 1: 1        Reps
 2: 01       ñ 2.5 mV Slow Range
 3: 1        DIFF Channel
 4: 1        Type T (Copper-Constantan)
 5: 1        Ref Temp Loc [ ref_temp  ]
 6: 5     -- Loc [ tc1       ]
 7: 1        Mult
 8: 0        Offset
; Sub-surface Ice Thermocouple string (0.1 m .. 9.1 m initial)

26:  End (P95)
; end loop of 10, thermocouple measurement loop.

27:  Beginning of Loop (P87)
 1: 0        Delay
 2: 2        Loop Count
; begin air temperature thermocouple multiplexer loop

28:  Do (P86)
 1: 71       Pulse Port 1

29:  Do (P86)
 1: 71       Pulse Port 1

30:  Thermocouple Temp (DIFF) (P14)
 1: 1        Reps
 2: 01       ñ 2.5 mV Slow Range
 3: 1        DIFF Channel
 4: 2        Type E (Chromel-Constantan)
 5: 1        Ref Temp Loc [ ref_temp  ]
 6: 15    -- Loc [ tc_air1   ]
 7: 1        Mult
 8: 0        Offset
; 2 x temperature mesurement

31:  End (P95)
; end air temperature thermocouple multiplexer loop

32:  Do (P86)
 1: 52       Set Port 2 Low
; turns multiplexer off, the remaining measurements
; will occur on the cr10 and not on the multiplexer.

33:  Pulse (P3)
 1: 2        Reps
 2: 1        Pulse Input Channel
 3: 21       Low Level AC, Output Hz
 4: 21       Loc [ U_1       ]
 5: 0.0980   Mult
 6: 0        Offset
; Young Wind Monitor wind speed -> m/s

; Output data in final storage every hour -----------------------------

34:  If time is (P92)
 1: 10       Minutes (Seconds --) into a
 2: 60       Interval (same units as above)
 3: 30       Then Do

35:  Data Transfer to HDR GOES (P126)
 1: 1        Overwrite Self Timed Buffer
 2: 0        Binary Format
 3: 40    -- Result Code Loc [ R126_RC   ]
; Transfer data to TX312 GOES transmitter

; check if the data has been sent to the transmitter ------------

36:  If (X<=>F) (P89)
 1: 40       X Loc [ R126_RC   ]
 2: 3        >=
 3: 1        F
 4: 30       Then Do

37:  If (X<=>F) (P89)
 1: 40       X Loc [ R126_RC   ]
 2: 4        <
 3: 7        F
 4: 30       Then Do

38:  Data Transfer to HDR GOES (P126)
 1: 1        Overwrite Self Timed Buffer
 2: 0        Binary Format
 3: 40    -- Result Code Loc [ R126_RC   ]
; Transfer data to TX312 GOES transmitter once more

39:  End (P95)

40:  End (P95)

; and once more
; check if the data has been sent to the transmitter ------------

41:  If (X<=>F) (P89)
 1: 40       X Loc [ R126_RC   ]
 2: 3        >=
 3: 1        F
 4: 30       Then Do

42:  If (X<=>F) (P89)
 1: 40       X Loc [ R126_RC   ]
 2: 4        <
 3: 7        F
 4: 30       Then Do

43:  Data Transfer to HDR GOES (P126)
 1: 1        Overwrite Self Timed Buffer
 2: 0        Binary Format
 3: 40    -- Result Code Loc [ R126_RC   ]
; Transfer data to TX312 GOES transmitter once more

44:  End (P95)

45:  End (P95)


46:  End (P95)

47:  If time is (P92)
 1: 00       Minutes (Seconds --) into a
 2: 60       Interval (same units as above)
 3: 10       Set Output Flag High
; send data to final storage each hour
; total data points send:

48:  Real Time (P77)^21999
 1: 1110     Year,Day,Hour/Minute (midnight = 0000)

49:  Average (P71)^7229
 1: 23       Reps
 2: 2        Loc [ sw_in     ]

50:  Average (P71)^22761
 1: 1        Reps
 2: 25       Loc [ pressure  ]

51:  Average (P71)^30737
 1: 2        Reps
 2: 26       Loc [ SD_1      ]

52:  Maximize (P73)^19263
 1: 2        Reps
 2: 0        Value Only
 3: 15       Loc [ tc_air1   ]

53:  Minimum (P74)^20039
 1: 2        Reps
 2: 00       Time Option
 3: 15       Loc [ tc_air1   ]

54:  Maximize (P73)^30778
 1: 2        Reps
 2: 0        Value Only
 3: 21       Loc [ U_1       ]

55:  Average (P71)^28698
 1: 1        Reps
 2: 1        Loc [ ref_temp  ]

56:  Sample (P70)^6808
 1: 1        Reps
 2: 28       Loc [ BATTERY   ]


57:  HDR GOES Status (P127)
 1: 0        Read Time
 2: 55       Result Code Loc [ t_msg     ]

58:  Sample (P70)^3805
 1: 1        Reps
 2: 56       Loc [ gmt_h     ]

59:  Sample (P70)^26988
 1: 1        Reps
 2: 57       Loc [ GMT_m     ]

60:  Sample (P70)^10819
 1: 1        Reps
 2: 58       Loc [ GMT_s     ]

61:  HDR GOES Status (P127)
 1: 2        Read Last Message Status
 2: 41       Result Code Loc [ Msg_1     ]

62:  Sample (P70)^18646
 1: 6        Reps
 2: 49       Loc [ lat_deg   ]

; read GPS reading:
; 49 = lat deg; 50 = lat min; 51 = lat sec
; 52 = lon deg; 53 = lon min; 54 = lon sec



63:  Serial Out (P96)
 1: 71       Storage Module

;*******************************Table 2 *********************************

*Table 2 Program
  02: 60        Execution Interval (seconds)
; 60 sec

;  check which presure sensor *******************************************

1:  Z=F (P30)
 1: 0        F
 2: 0        Exponent of 10
 3: 30       Z Loc [ p_flag    ]
;  set pressure sensor option. 0 is for the new cs106 = PTB110 pressure sensor
;  p_flag = 1 is for cs105 = PTB101B pressure sensor.

2:  Do (P86)
 1: 43       Set Port 3 High
; voltage control port 3, jumped to switched 12V
; for the purpose of switching on 12V to cs500

3:  Excitation with Delay (P22)
 1: 3        Ex Channel
 2: 0        Delay W/Ex (units = 0.01 sec)
 3: 100      Delay After Ex (units = 0.01 sec)
 4: 0        mV Excitation
; this only makes a delay, there is no excitation

4:  Volts (SE) (P1)
 1: 2        Reps
 2: 5        ñ 2500 mV Slow Range
 3: 3        SE Channel
 4: 17       Loc [ thmp_1    ]
 5: .1       Mult
 6: -40      Offset
; temperature cs500 probe x 2
; this is a single ended measurement so it is InChan 3
; removed 60 Hz rejection range since there is no 60 Hz
; power tools creating electrical noise.

5:  Volts (SE) (P1)
 1: 2        Reps
 2: 5        ñ 2500 mV Slow Range
 3: 5        SE Channel
 4: 19       Loc [ rh_1      ]
 5: .1       Mult
 6: 0        Offset
; humidity cs500 probe x 2

6:  Do (P86)
 1: 53       Set Port 3 Low
; Voltage control port 3 turned off, switched 12V
; to the cs-500 is turned off.


7:  Excite-Delay (SE) (P4)
 1: 2        Reps
 2: 5        ñ 2500 mV Slow Range
 3: 7        SE Channel
 4: 2        Excite all reps w/Exchan 2
 5: 2        Delay (units 0.01 sec)
 6: 2500     mV Excitation
 7: 23       Loc [ Dir_1     ]
 8: 0.142    Mult
 9: 0        Offset
; Young wind direction measurement

; Following a loop of the barometer measurement that is
; performed for the last minute of each hour.

8:  If time is (P92)
 1: 59       Minutes (Seconds --) into a
 2: 60       Interval (same units as above)
 3: 47       Set Port 7 High

; The pressure sensor has a range from 600 - 1060 hPa,mb
; -> multiplier = sensor range (460)/mV Excitation(2500)
; Multiplier = 0.184 for CS105; add 400 to value
; Multiplier = 0.240 for CS106; add 400 t value

9:  Z=F (P30)
 1: 0        F
 2: 0        Exponent of 10
 3: 32       Z Loc [ flag2     ]
; 0 in parameter location 1 sets flag2 to 0 for cs106
; 1 in parameter location 1 sets flag2 to 1 for cs105

10:  If time is (P92)
 1: 0        Minutes (Seconds --) into a
 2: 60       Interval (same units as above)
 3: 30       Then Do

11:  If (X<=>F) (P89)
 1: 30       X Loc [ p_flag    ]
 2: 1        =
 3: 0        F
 4: 30       Then Do

12:  Volt (SE) (P1)
 1: 1        Reps
 2: 25       2500 mV 60 Hz Rejection Range
 3: 9        SE Channel
 4: 25       Loc [ pressure  ]
 5: 0.240    Multiplier
 6: 100      Offset
;new vaisala cs106 pressure sensor
; offset is 500, need to offset by -400
; add +400 to process data

13:  Batt Voltage (P10)
 1: 28       Loc [ BATTERY   ]

14:  End (P95)
; end cs106 new pressure sensor measurement last minute of each hr

15:  If (X<=>F) (P89)
 1: 30       X Loc [ p_flag    ]
 2: 1        =
 3: 1        F
 4: 30       Then Do

16:  Volts (SE) (P1)
 1: 1        Reps
 2: 25       2500 mV 60 Hz Rejection Range
 3: 9        SE Channel
 4: 25       Loc [ pressure  ]
 5: .184     Mult
 6: 600      Offset
; CS105 Vaisala Barometer
; another 400 has to be added to pressure value to yield
; mb this can be done in post processing.

17:  Batt Voltage (P10)
 1: 28       Loc [ BATTERY   ]

18:  Do (P86)
 1: 57       Set Port 7 Low

19:  End (P95)
; end cs105 pressure sensor measurement last minute of each hr

20:  End (P95)
; end pressure sensor measurement last minute of each hr

; Following is the snow height measurement which
; occurs each 10 minutes, nested in a 15 second
; measurement interval.

21:  If time is (P92)
 1: 0        Minutes (Seconds --) into a
 2: 10       Interval (same units as above)
 3: 30       Then Do

22:  SDI-12 Recorder (OS10 1.1) (P105)
 1: 0        SDI-12 Address
 2: 0        Start Measurement (aM0!)
 3: 4        Port
 4: 26       Loc [ SD_1      ]
 5: 1        Mult
 6: 0        Offset
; sonic snow height sensor 1

23:  SDI-12 Recorder (OS10 1.1) (P105)
 1: 0        SDI-12 Address
 2: 0        Start Measurement (aM0!)
 3: 5        Port
 4: 27       Loc [ SD_2      ]
 5: 1        Mult
 6: 0        Offset
; sonic snow height sensor 2

24:  End (P95)

*Table 3 Subroutines

End Program















-Input Locations-
1 ref_temp  1 6 3
2 sw_in     1 2 2
3 sw_ref    1 2 1
4 net_rad   1 1 1
5 tc1       1 1 1
6 tc2       1 0 0
7 tc3       1 0 0
8 tc4       1 0 0
9 tc5       1 0 0
10 tc6       1 0 0
11 tc7       1 0 0
12 tc8       1 0 0
13 tc9       1 0 0
14 tc10      1 0 0
15 tc_air1   1 3 1
16 tc_air2   1 3 0
17 thmp_1    1 0 0
18 thmp_2    17 0 1
19 rh_1      5 0 1
20 rh_2      17 0 1
21 U_1       1 1 1
22 U_2       1 1 1
23 Dir_1     5 0 1
24 Dir_2     17 0 1
25 pressure  1 1 2
26 SD_1      1 1 1
27 SD_2      1 0 1
28 BATTERY   1 1 2
29 _________ 1 0 0
30 p_flag    1 2 1
31 _________ 1 0 0
32 flag2     1 0 1
33 alpha1    1 1 1
34 _________ 1 0 1
35 _________ 1 0 1
36 _________ 1 0 1
37 _________ 9 0 1
38 _________ 1 0 1
39 _________ 1 0 1
40 R126_RC   9 4 4
41 Msg_1     5 0 2
42 Msg_2     9 0 2
43 Msg_3     9 0 2
44 Msg_4     9 0 2
45 Msg_5     9 0 2
46 power     9 0 2
47 GPS_time  9 0 1
48 Msg_8     9 0 1
49 lat_deg   9 1 1
50 lat_min   9 1 1
51 lat_ses   9 1 1
52 lon_deg   9 1 1
53 lon_min   9 1 1
54 lon_sec   17 1 1
55 t_msg     5 0 1
56 gmt_h     9 1 1
57 GMT_m     9 1 1
58 GMT_s     17 1 1
59 CSI_R     0 0 0
60 _________ 0 0 0
61 _________ 0 0 0
62 _________ 0 0 0
63 _________ 0 0 0
64 _________ 0 0 0
65 _________ 0 0 0
66 _________ 0 0 0
67 _________ 0 0 0
68 _________ 0 0 0
69 _________ 0 0 0
70 _________ 0 0 0
71 _________ 0 0 0
72 _________ 0 0 0
73 _________ 0 0 0
74 _________ 0 0 0
75 _________ 0 0 0
76 _________ 0 0 0
77 _________ 0 0 0
78 _________ 0 0 0
79 _________ 0 0 0
80 _________ 0 0 0
81 _________ 0 0 0
82 _________ 0 0 0
83 _________ 0 0 0
84 _________ 0 0 0
85 _________ 0 0 0
86 _________ 0 0 0
87 _________ 0 0 0
88 _________ 0 0 0
89 _________ 0 0 0
90 _________ 1 0 0
91 _________ 1 0 0
92 _________ 1 0 0
93 _________ 1 0 0
94 _________ 1 0 0
-Program Security-
0000
0000
0000
-Mode 4-
-Final Storage Area 2-
0
-CR10X ID-
0
-CR10X Power Up-
3
-CR10X Compile Setting-
3
-CR10X RS-232 Setting-
-1
-DLD File Labels-
0
-Final Storage Labels-
0,Year_RTM,21999
0,Day_RTM
0,Hour_Minute_RTM
1,sw_in_AVG~2,7229
1,sw_ref_AVG~3
1,net_rad_AVG~4
1,tc1_AVG~5
1,tc2_AVG~6
1,tc3_AVG~7
1,tc4_AVG~8
1,tc5_AVG~9
1,tc6_AVG~10
1,tc7_AVG~11
1,tc8_AVG~12
1,tc9_AVG~13
1,tc10_AVG~14
1,tc_air1_AVG~15
1,tc_air2_AVG~16
1,thmp_1_AVG~17
1,thmp_2_AVG~18
1,rh_1_AVG~19
1,rh_2_AVG~20
1,U_1_AVG~21
1,U_2_AVG~22
1,Dir_1_AVG~23
1,Dir_2_AVG~24
2,pressure_AVG~25,22761
3,SD_1_AVG~26,30737
3,SD_2_AVG~27
4,U_1_MAX~21,30778
4,U_2_MAX~22
5,BATTERY~28,6808
6,gmt_h~56,3805
7,lat_deg~49,18646
7,lat_min~50
7,lat_ses~51
7,lon_deg~52
7,lon_min~53
7,lon_sec~54
8,tc_air1_MAX~15,19263
8,tc_air2_MAX~16
9,tc_air1_MIN~15,20039
9,tc_air2_MIN~16
10,ref_temp_AVG~1,28698
11,GMT_m~57,26988
12,GMT_s~58,10819
